plugins {
    id "java"
    id "idea"
    id "application"
    id "org.openjfx.javafxplugin" version "0.0.8"
    id "org.beryx.jlink" version "2.20.0"
}

configure(project) {
    archivesBaseName = "noexs"
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

application {
    mainModule = "$moduleName"
    mainClass = "me.mdbell.noexs.Main"
}

dependencies {
    compile project(":noexs-utils")

    compile "org.usb4java:usb4java:1.3.0"
    compile "org.usb4java:usb4java-javax:1.3.0"
    compile "com.google.code.gson:gson:2.8.5"
}

javafx {
    version = "14.0.1"
    modules = ["javafx.base", "javafx.graphics", "javafx.controls", "javafx.fxml"]
}

jlink {
    options = ["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"]
    launcher {
        name = project.archivesBaseName
    }
    imageZip = file("$buildDir/image-zip/noexs.zip")
}

jar {
    manifest {
        attributes(
            'Main-Class': "me.mdbell.noexs.Main",
            'Implementation-Title': project.name,
            'Implementation-Vendor': project.group,
            'Implementation-Version': project.version,
            'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

task fatJar(type: Jar) {
    classifier 'all'

    from(configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/rxjava.properties'
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    with jar
    manifest.from jar.manifest
}

artifacts {
    archives fatJar
}
